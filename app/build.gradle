apply plugin: 'com.android.application'

dependencies {
	api project(':lib')
}

android {
	compileSdkVersion 34
	buildToolsVersion '34.0.0'
	defaultConfig {
        applicationId 'com.litehalls.mupdf.viewer.app'
        namespace 'com.litehalls.mupdf.viewer.app'
		minSdkVersion 21
		targetSdkVersion 34
		versionName '1.00'
		versionCode 1

        if (!project.hasProperty('noSplits')) {
	        splits {
		        abi {
			        enable true
			        reset()
			        include('arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64')
			        universalApk true
		        }
	        }
        }
        else if (project.hasProperty('ABI_FILTERS')) {
            ndk {
                abiFilters = []
                abiFilters.addAll(ABI_FILTERS.split(',').collect{it as String})
            }
        }
	}
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
	if (project.hasProperty('release_storeFile')) {
		signingConfigs {
			release {
				enableV3Signing true
				enableV4Signing true
				storeFile file(release_storeFile)
				storePassword release_storePassword
				keyAlias release_keyAlias
				keyPassword release_keyPassword
			}
		}
		// ndkVersion "25.1.8937393"
		buildTypes {
			release {
				signingConfig signingConfigs.release
				// minifyEnabled true
				// shrinkResources true
				// proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
				ndk {
					debugSymbolLevel 'FULL'
				}
			}
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}
